ext {
    junitVersion = '4.13.2'
    androidxAppCompatVersion = '1.6.1'
    androidxJunitVersion = '1.1.5'
    androidxEspressoCoreVersion = '3.5.1'
}

buildscript {
    ext.kotlin_version = '1.9.20'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace "com.flixcapacitor.torrent"
    compileSdk project.hasProperty('compileSdkVersion') ? rootProject.ext.compileSdkVersion : 34

    defaultConfig {
        minSdkVersion project.hasProperty('minSdkVersion') ? rootProject.ext.minSdkVersion : 22
        targetSdkVersion project.hasProperty('targetSdkVersion') ? rootProject.ext.targetSdkVersion : 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

repositories {
    google()
    mavenCentral()
    maven {
        url 'https://dl.frostwire.com/maven'
        content {
            includeGroup 'com.frostwire'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':capacitor-android')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // jlibtorrent - Native torrent engine (from FrostWire Maven)
    // Using 2.0.12.5 - same version as FrostWire Android (built with NDK r28c, 16KB pages)
    def jlibtorrent_version = '2.0.12.5'
    implementation "com.frostwire:jlibtorrent:$jlibtorrent_version"
    // ANDROID (needs all of them for most projects if you want to maximize compatibility)
    implementation "com.frostwire:jlibtorrent-android-arm:$jlibtorrent_version"
    implementation "com.frostwire:jlibtorrent-android-arm64:$jlibtorrent_version"
    implementation "com.frostwire:jlibtorrent-android-x86:$jlibtorrent_version"
    implementation "com.frostwire:jlibtorrent-android-x86_64:$jlibtorrent_version"

    // NanoHTTPD - Embedded HTTP server
    implementation 'org.nanohttpd:nanohttpd:2.3.1'

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
}
